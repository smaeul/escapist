# Maintainer: Samuel Holland <samuel@sholland.net>

pkgname=stage2-curl
pkgver=7.33.0
pkgrel=1
pkgdesc="An URL retrieval utility and library"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=('MIT')
makedepends=('stage1-toolchain' 'stage2-zlib')
#options=('strip' 'debug')
source=("http://curl.haxx.se/download/curl-$pkgver.tar.gz"{,.asc}
        "0001-glob-fix-regression-from-commit-5ca96cb844.patch::https://github.com/bagder/curl/commit/867b52a.patch"
        curlbuild.h)
md5sums=('c8a4eaac7ce7b0d1bf458d62ccd4ef93'
         'SKIP'
         'd71c7eab7d38f1731f78285634a4c390'
         '751bd433ede935c8fae727377625a8ae')

prepare() {
  cd "$srcdir/curl-$pkgver"

  patch -Np1 < "$srcdir"/0001-glob-fix-regression-from-commit-5ca96cb844.patch
}

build() {
  cd "$srcdir/curl-$pkgver"

  ./configure \
      --prefix=/stage1 \
      --mandir=/stage1/share/man \
      --disable-ldap \
      --disable-ldaps \
      --enable-ipv6 \
      --enable-manual \
      --enable-versioned-symbols \
      --enable-threaded-resolver \
      --without-libidn \
      --with-random=/dev/urandom \
#      --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt

  make
}

package() {
  cd "$srcdir/curl-$pkgver"

  make DESTDIR="$pkgdir" install

  local ptrsize=$(cpp <<<'__SIZEOF_POINTER__' | sed '/^#/d')
  case $ptrsize in
    8) _curlbuild=curlbuild-64.h ;;
    4) _curlbuild=curlbuild-32.h ;;
    *) error "unknown pointer size for architecture: %s bytes" "$ptrsize"
      exit 1
      ;;
  esac

  # license
  install -Dm644 COPYING "$pkgdir/stage1/share/licenses/$pkgname/COPYING"

  # devel
  mv "$pkgdir/stage1/include/curl/curlbuild.h" "$pkgdir/stage1/include/curl/$_curlbuild"
  install -m644 "$srcdir/curlbuild.h" "$pkgdir/stage1/include/curl/curlbuild.h"
}
