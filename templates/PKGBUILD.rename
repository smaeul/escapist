_srcname=mpfr
_srcver=3.1.2
_srcpatch=5
pkgname=lib${_srcname}
pkgver=${_srcver}.${_srcpatch}
pkgrel=1
pkgdesc="Multiprecision floating-point library"
arch=('i686' 'x86_64')
url="http://mpfr.org/"
license=('LGPL')
depends=('libgmp')
source=(http://mpfr.org/${_srcname}-current/${_srcname}-${_srcver}.tar.xz{,.asc}
        ${_srcname}-${_srcver}-p${_srcpatch}.patch
        ${pkgname}-musl.patch)

prepare() {
  cd "${srcdir}/${_srcname}-${_srcver}"

  patch -p1 -i "${srcdir}/${_srcname}-${_srcver}-p${_srcpatch}.patch"
  patch -p1 -i "${srcdir}/${pkgname}-musl.patch"
}

build() {
  cd "${srcdir}/${_srcname}-${_srcver}"

  ./configure --build=$CHOST --prefix= \
    --disable-shared --enable-thread-safe
  make
}

check() {
  cd "${srcdir}/${_srcname}-${_srcver}"

  make check
}

package() {
  cd "${srcdir}/${_srcname}-${_srcver}"

  make DESTDIR="${pkgdir}" install
}
