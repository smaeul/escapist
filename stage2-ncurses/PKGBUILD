# Maintainer: Samuel Holland <samuel@sholland.net>

pkgname=stage2-ncurses
pkgver=5.9
pkgrel=1
pkgdesc="System V Release 4.0 curses emulation library"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
makedepends=('stage1-toolchain')
source=(http://ftp.gnu.org/pub/gnu/ncurses/ncurses-${pkgver}.tar.gz{,.sig})
md5sums=('8cb9c412e5f2d96bc6f459aa8c6282a1'
         '014ffdbbfec6d41a9a89d6cbe6434638')

prepare() {
  rm -rf ${srcdir}/ncurses-build
  mkdir ${srcdir}/ncurses-build
}

build() {
  cd ${srcdir}/ncurses-build

  ../ncurses-${pkgver}/configure \
    --build=$CHOST --prefix=/stage1 --mandir=/stage1/share/man \
    --without-shared --with-normal --without-debug --without-ada \
    --enable-widec --disable-pc-files --enable-ext-colors --enable-ext-mouse
  make
}

package() {
  cd ${srcdir}/ncurses-build
  make DESTDIR=${pkgdir} install

  # fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses ncurses++ form panel menu; do
    ln -s lib${lib}w.a ${pkgdir}/stage1/lib/lib${lib}.a
#    ln -s ${lib}w.pc ${pkgdir}/stage1/lib/pkgconfig/${lib}.pc
  done

  # some packages look for -lcurses during build
  ln -s libncursesw.a ${pkgdir}/stage1/lib/libcursesw.a
  ln -s libncursesw.a ${pkgdir}/stage1/lib/libcurses.a

  # install license, rip it from the readme
  cd ${srcdir}/ncurses-${pkgver}
  install -dm755 ${pkgdir}/stage1/share/licenses/$pkgname
  grep -B 100 '$Id' README > ${pkgdir}/stage1/share/licenses/${pkgname}/license.txt
}
