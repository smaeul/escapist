# Maintainer: Samuel Holland <samuel@sholland.net>

pkgname=('stage0-gcc')
pkgver=4.8.1
pkgrel=1
pkgdesc="Temporary static GCC and libs - Starch stage 0"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
url="http://gcc.gnu.org"
depends=('stage0-binutils')
makedepends=('stage0-gmp' 'stage0-mpc' 'stage0-mpfr')
options=('staticlibs' '!emptydirs')
source=(http://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
        gcc-4.8.1-musl.patch)
md5sums=(SKIP
         SKIP)

prepare() {
  cd ${srcdir}/gcc-${pkgver}

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # No need for lib64 folder; no multilib in stage0
  [[ $CARCH == "x86_64" ]] && sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

  # https://github.com/GregorR/musl-cross
  patch -Np1 -i ${srcdir}/gcc-4.8.1-musl.patch

  for file in \
    $(find gcc/config -name linux64.h -o -name linux.h -o -name sysv4.h)
  do
    sed -e 's@/lib\(64\)\?\(32\)\?/ld@&@g' \
        -e 's@/usr@@g' -i $file
    echo '
#undef STANDARD_STARTFILE_PREFIX_1
#undef STANDARD_STARTFILE_PREFIX_2
#define STANDARD_STARTFILE_PREFIX_1 "/lib/"
#define STANDARD_STARTFILE_PREFIX_2 ""' >> $file
  done

  rm -rf ${srcdir}/gcc-build
  mkdir ${srcdir}/gcc-build
}

build() {
  cd ${srcdir}/gcc-build

  # using -pipe causes spurious test-suite failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=48565
  CFLAGS=${CFLAGS/-pipe/}
  CXXFLAGS=${CXXFLAGS/-pipe/}

  ${srcdir}/gcc-${pkgver}/configure \
    --target=$CHOST --prefix=/stage0 --libexecdir=/stage0/lib \
    --disable-shared --disable-threads --disable-multilib --disable-werror \
    --disable-nls --disable-libssp --disable-libmudflap --disable-libquadmath \
    --disable-libgomp --disable-libitm --disable-libatomic --disable-libsanitizer \
    --disable-libstdc++-v3 \
    --disable-decimal-float --enable-clocale=generic --enable-languages=c,c++ \
    --with-sysroot=/stage1 --with-newlib --without-headers \
    --with-local-prefix=/ --with-native-system-header-dir=/include \
    --with-gmp=/stage0 --with-mpfr=/stage0 --with-mpc=/stage0 \

  make
}

package() {
  cd ${srcdir}/gcc-build

  make DESTDIR=${pkgdir} install

  ln -s $CHOST-gcc ${pkgdir}/stage0/bin/$CHOST-cc
  ln -s libgcc.a ${pkgdir}/stage0/lib/gcc/$CHOST/${pkgver}/libgcc_eh.a

  rm ${pkgdir}/stage0/lib/libiberty.a
}
