# Maintainer: Samuel Holland <samuel@sholland.net>

pkgname=stage2-sh
pkgver=R40b
pkgrel=1
pkgdesc="The MirOS KornShell - an enhanced version of the public domain ksh"
url="http://mirbsd.de/mksh"
license=('MirOS') # see http://www.opensource.org/licenses/miros.html
makedepends=('stage1-toolchain' 'stage2-ncurses' 'stage2-readline' 'cpio' 'groff' 'sh')
provides=('sh')
arch=('i686' 'x86_64')
install=mksh.install
source=(http://www.mirbsd.org/MirOS/dist/mir/mksh/mksh-$pkgver.cpio.gz)
md5sums=('afb08b65272ace550ec59b26a876a7de')

prepare() {
  cd $srcdir

  # makepkg can't extract cpio files
  cpio -mid < mksh-$pkgver.cpio
}

build() {
  cd $srcdir/mksh

  sh Build.sh -r -c lto
  ./mksh test.sh
}

package() {
  cd $srcdir/mksh

  mkdir -p $pkgdir/stage1/bin
  mkdir -p $pkgdir/stage1/share/man/man1
  mkdir -p $pkgdir/stage1/share/mksh
  install -c -s -o root -g root -m 555 mksh $pkgdir/stage1/bin/mksh
  install -c -o root -g root -m 644 mksh.1 $pkgdir/stage1/share/man/man1/mksh.1
  install -c -o root -g root -m 644 dot.mkshrc $pkgdir/stage1/share/mksh/dot.mkshrc
  ln -s mksh $pkgdir/stage1/bin/sh
}
