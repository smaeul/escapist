pkgname=make
pkgver=3.82
pkgrel=1
pkgdesc="GNU utility to make programs"
arch=('i686' 'x86_64')
url="http://gnu.org/software/make"
license=('GPL3')
depends=('sh')
groups=('devel')
source=(http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig}
        make-3.82-bug30612.patch
        make-3.82-bug30723.patch
        make-3.82-makeflags.patch
        make-3.82-sort-blank.patch
        ${pkgname}-musl.patch)

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/${pkgname}-musl.patch"
  patch -p1 -i ${srcdir}/make-3.82-bug30612.patch
  patch -p1 -i ${srcdir}/make-3.82-bug30723.patch
  patch -p1 -i ${srcdir}/make-3.82-makeflags.patch
  patch -p1 -i ${srcdir}/make-3.82-sort-blank.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --build=$CHOST --prefix= \
    --disable-nls
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make check
}
  
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
