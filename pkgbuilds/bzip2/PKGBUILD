pkgbase=bzip2
pkgname=(bzip2 libbz2)
pkgrel=1
pkgver=1.0.6
pkgdesc="bzip2 compression utility"
arch=(i686 x86_64)
license=(custom)
url="http://www.bzip2.org/"
source=(http://www.bzip.org/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('a2848f34fcd5d6cf47def00461fcb528a0484d8edef8208d6d2e2909dc61d9cd')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  # Override optimization flags
  sed -i "s@-O2@${CFLAGS}@g" Makefile
}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make bzip2 bzip2recover
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make test
}

package_bzip2() {
  groups=(devel)
  depends=(sh)

  cd ${srcdir}/${pkgbase}-${pkgver}

  install -dm755 ${pkgdir}/{bin,share/man/man1}
  install -Dm755 bzip2 ${pkgdir}/bin/bzip2
  install -Dm755 bzip2recover bzgrep bzmore bzdiff ${pkgdir}/bin
  ln -s bzip2 ${pkgdir}/bin/bunzip2
  ln -s bzip2 ${pkgdir}/bin/bzcat
  ln -s bzdiff ${pkgdir}/bin/bzcmp
  ln -s bzgrep ${pkgdir}/bin/bzegrep
  ln -s bzgrep ${pkgdir}/bin/bzfgrep
  ln -s bzmore ${pkgdir}/bin/bzless
  install -Dm644 bz{ip2,diff,grep,more}.1 ${pkgdir}/share/man/man1
  ln -s bzip2.1 ${pkgdir}/share/man/man1/bunzip2.1
  ln -s bzip2.1 ${pkgdir}/share/man/man1/bzcat.1
  ln -s bzip2.1 ${pkgdir}/share/man/man1/bzip2recover.1
  install -Dm644 LICENSE ${pkgdir}/share/licenses/custom/${pkgname}
}

package_libbz2() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  install -Dm644 bzlib.h ${pkgdir}/include/bzlib.h
  install -Dm644 libbz2.a ${pkgdir}/lib/libbz2.a
  install -Dm644 LICENSE ${pkgdir}/share/licenses/custom/${pkgname}
}
