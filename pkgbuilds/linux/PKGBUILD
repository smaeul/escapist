pkgbase=linux
pkgname=(linux linux-api-headers)
pkgrel=1
pkgver=3.18.1
pkgdesc="The Linux kernel and modules"
arch=(i686 x86_64)
license=(GPL2)
url="https://www.kernel.org/"
makedepends=(bc linux-firmware)
#install=linux.install
source=(https://www.kernel.org/pub/linux/kernel/v3.x/${pkgbase}-${pkgver}.tar.xz
        ${pkgbase}-posix-sed.patch
        config.x86_64)
sha256sums=('5833cd780fd96614b722040041d8242c102ad7eb956d6f102760bc0faad0a08e'
            'cb6236335f86cd2491c1c5f5d298a7437ac250aab101c9b9ad69c5cae252c0f9'
            'fc47174eb8c695cbcd42334087e1a9ae3d099aea0793bf92014c085e3401b02a')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  patch -p1 -i ${srcdir}/${pkgbase}-posix-sed.patch

  # set extraversion to pkgrel
  sed -ri "s|^(EXTRAVERSION =).*|\1 -${pkgrel}|" Makefile

  make mrproper
  cp ${srcdir}/config.$CARCH .config
  make oldconfig
}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make LOCALVERSION= bzImage
}

package_linux() {
  groups=(base)
  options=(!strip)

  cd ${srcdir}/${pkgbase}-${pkgver}

  install -Dm644 arch/x86/boot/bzImage ${pkgdir}/boot/vmlinuz-${pkgname}
  install -Dm644 System.map ${pkgdir}/boot/System.map-${pkgname}
}

package_linux-api-headers() {
  groups=(devel)

  cd ${srcdir}/${pkgbase}-${pkgver}

  make INSTALL_HDR_PATH=${pkgdir} headers_install
  find ${pkgdir} -name '*.install*' -delete
}
