pkgbase=bash
pkgname=${pkgbase}
pkgrel=1
pkgver=4.3.30
pkgdesc="The Bourne-again shell"
arch=(i686 x86_64)
groups=(base)
license=(GPL)
url="http://www.bash.org/"
depends=(ncurses readline)
makedepends=(bison libncursesw libreadline)
provides=(sh)
backup=(etc/bash{_logout,rc}
        etc/skel/.bash{_logout,_profile,rc})
source=(http://ftp.gnu.org/gnu/${pkgbase}/${pkgbase}-${pkgver}.tar.gz{,.sig}
        system.bash{_logout,rc}
        user.bash{_logout,_profile,rc})
sha256sums=('317881019bbf2262fb814b7dd8e40632d13c3608d2f237800a8828fbb8a640dd'
            'SKIP'
            '24bceefc3d60d270bc4e4bc1fb10d83b16989c49d069579bbbe28ccb1440e290'
            '6b613c3dcf4c7e18be9bce1f5f893c49a40285423e81d6501b5cb938a69a5593'
            '4330edf340394d0dae50afb04ac2a621f106fe67fb634ec81c4bfb98be2a1eb5'
            'e149407c2bee17779caec70a7edd3d0000d172e7e4347429b80cb4d55bcec9c2'
            '3e22bf86ae6708df7a6bceb88c67a00118275f9c0b5268f453dd388af7c43b53')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  CFLAGS="${CFLAGS} -DDEFAULT_PATH_VALUE='\"/bin\"'
    -DSTANDARD_UTILS_PATH='\"/bin\"' -DSYS_BASH_LOGOUT='\"/etc/bash_logout\"'
    -DSYS_BASHRC='\"/etc/bashrc\"'" \
  ./configure --build=$CHOST \
    --prefix= \
    --with-curses --with-installed-readline --without-bash-malloc \
    --enable-readline
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make check
}

package() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make DESTDIR=${pkgdir} install

  ln -s /bin/bash ${pkgdir}/bin/sh

  install -Dm644 ${srcdir}/system.bash_logout ${pkgdir}/etc/bash_logout
  install -Dm644 ${srcdir}/system.bashrc ${pkgdir}/etc/bashrc
  install -Dm644 ${srcdir}/user.bash_profile ${pkgdir}/etc/skel/.bash_profile
  install -Dm644 ${srcdir}/user.bash_logout ${pkgdir}/etc/skel/.bash_logout
  install -Dm644 ${srcdir}/user.bashrc ${pkgdir}/etc/skel/.bashrc
}
