pkgbase=ncurses
pkgname=(${pkgbase} lib${pkgbase}w)
pkgrel=1
pkgver=5.9
pkgdesc="Ncurses terminal drawing library"
arch=(i686 x86_64)
license=(MIT)
url="http://www.gnu.org/software/ncurses/"
makedepends=(pkg-config)
source=(http://ftp.gnu.org/gnu/${pkgbase}/${pkgbase}-${pkgver}.tar.gz{,.sig}
        ${pkgbase}-musl.patch)
sha256sums=('9046298fb440324c9d4135ecea7879ffed8546dd1b58e59430ea07a4633f563b'
            'SKIP'
            '03b8d6cad9431a6a1bcdb7959df90e3883a2786ddfa2257003d04eef7e0db44d')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  patch -p1 -i ${srcdir}/${pkgbase}-musl.patch
}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  ./configure --build=$CHOST \
    --prefix= --mandir=/share/man \
    --without-shared --with-normal --without-debug --with-cxx --without-ada \
    --enable-widec --enable-ext-colors --enable-ext-mouse --enable-overwrite \
    --enable-pc-files
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make -C test
}

package_ncurses() {
  groups=(base)

  cd ${srcdir}/${pkgbase}-${pkgver}

  make DESTDIR=${pkgdir} install

  rm -r ${pkgdir}/{include,lib,share/man/man3}
  install -dm755 ${pkgdir}/lib
  ln -s ../share/terminfo ${pkgdir}/lib/terminfo
}

package_libncursesw() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make DESTDIR=${pkgdir} install

  for lib in form menu ncurses panel
  do
    ln -s lib${lib}w.a ${pkgdir}/lib/lib${lib}.a
  done
  for lib in curses cursesw
  do
    ln -s libncursesw.a ${pkgdir}/lib/lib${lib}.a
  done

  rm -r ${pkgdir}/{bin,share/{man/man{1,5,7},tabset,terminfo}}
  rm ${pkgdir}/lib/terminfo
}
