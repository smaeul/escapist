pkgbase=pacman
pkgname=(${pkgbase} libalpm)
pkgrel=1
pkgver=4.1.2
pkgdesc="An advanced package manager from Arch Linux"
arch=(i686 x86_64)
license=(GPL)
url="http://www.archlinux.org/pacman/"
checkdepends=(python)
makedepends=(libarchive libcurl libgpgme libressl m4 pkg-config)
source=(ftp://ftp.archlinux.org/other/${pkgbase}/${pkgbase}-${pkgver}.tar.gz{,.sig}
        ${pkgbase}-musl.patch)
sha256sums=('7c059b444735ee254cfbd8dc2fac8c5d61453601f8adaf001c7038cdc673334c'
            'SKIP'
            'b0f97c583852653ab1bb0807d0232b87cee3d2e9b5d9ba0fb79a2451dad3d8fb')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  patch -p1 -i ${srcdir}/${pkgbase}-musl.patch

  sed -i.bak -e 's/ --apparent-size//' configure
  sed -i.bak -e 's/ --follow-symlinks//' configure
}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  DUPATH=du \
  ./configure --build=$CHOST \
    --prefix= --sysconfdir=/etc --localstatedir=/var \
    --with-scriptlet-shell=/bin/bash \
    --with-pkg-ext=.pkg.tar.xz --with-src-ext=.src.tar.xz \
    --disable-doc --disable-shared
  make
  make -C contrib
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make check
}

package_pacman() {
  groups=(base)
  depends=(bash gnupg)
  backup=(etc/makepkg.conf
          etc/pacman.conf)

  cd ${srcdir}/${pkgbase}-${pkgver}

  make DESTDIR=${pkgdir} install
  make DESTDIR=${pkgdir} -C contrib install
  rm -r ${pkgdir}/{include,lib}

  install -dm755 ${pkgdir}/share/bash-completion/completions
  mv ${pkgdir}/etc/bash_completion.d/pacman ${pkgdir}/share/bash-completion/completions
  rmdir ${pkgdir}/etc/bash_completion.d
  for f in makepkg pacman-key
  do
    ln -s pacman ${pkgdir}/share/bash-completion/completions/$f
  done

  install -Dm644 contrib/PKGBUILD.vim ${pkgdir}/share/vim/vimfiles/syntax/PKGBUILD.vim

  sed -i 's/-pc-linux-musl/-linux-musl/g' ${pkgdir}/etc/makepkg.conf
}

package_libalpm() {
  depends=(libarchive libcurl libgpgme libressl)

  cd ${srcdir}/${pkgbase}-${pkgver}

  make DESTDIR=${pkgdir} install
  rm -r ${pkgdir}/{etc,bin,share}
}
