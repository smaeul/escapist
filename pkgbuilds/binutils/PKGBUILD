pkgbase=binutils
pkgname=${pkgbase}
pkgrel=1
pkgver=2.24
pkgdesc="Utilities for assembling, linking, and manipulating object files"
arch=(i686 x86_64)
groups=(devel)
license=(GPL)
url="http://www.gnu.org/software/binutils/"
# NOTE: configure won't detect libfl without flex
makedepends=(flex libfl libz)
source=(http://ftp.gnu.org/gnu/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2{,.sig})
sha256sums=('e5e8c5be9664e7f7f96e0d09919110ab5ad597794f5b1809871177a0f0f14137'
            'SKIP')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  rm -rf ${srcdir}/${pkgbase}-build
  mkdir ${srcdir}/${pkgbase}-build
}

build() {
  cd ${srcdir}/${pkgbase}-build

  ${srcdir}/${pkgbase}-${pkgver}/configure --build=$CHOST --host=$CHOST \
    --prefix= \
    --enable-threads --disable-shared \
    --with-lib-path=/lib
  make tooldir=
}

check() {
  cd ${srcdir}/${pkgbase}-build

  make check
}

package() {
  cd ${srcdir}/${pkgbase}-build

  make DESTDIR=${pkgdir} tooldir= install
}
