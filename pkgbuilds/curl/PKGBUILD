pkgbase=curl
pkgname=(curl libcurl)
pkgrel=1
pkgver=7.39.0
pkgdesc="URL retrieval library and utility"
arch=(i686 x86_64)
license=(MIT)
url="http://www.curl.org/"
makedepends=(libressl libz)
source=(http://curl.haxx.se/download/${pkgbase}-${pkgver}.tar.gz{,.asc}
        curlbuild.h)
sha256sums=('a3a7c2b9e7416c728469eb4cb5b61e9161335df4278329e1d9cc3c194e25d795'
            'SKIP'
            'f9e2602927321a9c410019b0dd7b1c02f913c02ce4534d5c9962da270f784b49')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  PATH_SEPARATOR=: \
  ./configure --build=$CHOST \
    --prefix= --mandir=/share/man \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
    --enable-ipv6 --enable-optimize --enable-symbol-hiding \
    --enable-threaded-resolver \
    --disable-shared
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make check
}

package_curl() {
  groups=(devel net)

  cd ${srcdir}/${pkgbase}-${pkgver}

  make DESTDIR=${pkgdir} install-exec
  rm -r ${pkgdir}/lib
}

package_libcurl() {
  depends=(libressl libz)

  cd ${srcdir}/${pkgbase}-${pkgver}

  make DESTDIR=${pkgdir} install
  rm -r ${pkgdir}/bin

  case $CARCH in
    x86_64) _cb=curlbuild-64.h ;;
    *) _cb=curlbuild-32.h ;;
  esac
  mv ${pkgdir}/include/curl/{curlbuild.h,${_cb}}
  install -Dm644 ${srcdir}/curlbuild.h ${pkgdir}/include/curl/curlbuild.h
}
