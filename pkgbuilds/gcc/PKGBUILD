pkgbase=gcc
pkgname=(gcc libgcc libstdc++)
pkgrel=1
pkgver=4.7.4
pkgdesc="GNU Compiler Collection"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnu.org/software/gcc/"
makedepends=(libgmp libmpc libmpfr libz)
source=(http://ftp.gnu.org/gnu/${pkgbase}/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.bz2{,.sig}
        ${pkgbase}-musl.patch)
sha256sums=('92e61c6dc3a0a449e62d72a38185fda550168a86702dea07125ebd3ec3996282'
            'SKIP'
            '350c0fa9b1995080616bf577590ad7ee42360da0ba7d5767112141c093040cbf')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  patch -p1 -i ${srcdir}/${pkgbase}-musl.patch

  for file in $(find gcc/config -name *linux* -o -name *sysv4*) gcc/genmultilib
  do
    sed -i.bak -e 's@/lib64@/lib@g' "$file"
  done

  rm -rf ${srcdir}/${pkgbase}-build
  mkdir ${srcdir}/${pkgbase}-build
}

build() {
  cd ${srcdir}/${pkgbase}-build

  ${srcdir}/${pkgbase}-${pkgver}/configure --build=$CHOST \
    --prefix= --libexecdir=/lib --mandir=/share/man \
    --with-linker-build-id --with-native-system-header-dir=/include --with-system-zlib \
    --enable-__cta_atexit --enable-clocale=generic --enable-languages=c,c++ \
    --enable-threads=posix \
    --disable-bootstrap --disable-libstdcxx-pch --disable-lto-plugin --disable-multilib \
    --disable-shared
  make all-gcc all-target-libgcc all-target-libstdc++-v3
}

check() {
  cd ${srcdir}/${pkgbase}-build

  make check
}

package_gcc() {
  groups=(devel)
  depends=(binutils libgcc libstdc++)

  cd ${srcdir}/${pkgbase}-build

  make DESTDIR=${pkgdir} install-gcc

  # Symlink for compatibility
  ln -s gcc ${pkgdir}/bin/cc

  # POSIX conformance launcher scripts for c89 and c99
  cat > $pkgdir/bin/c89 <<"EOF"
#!/bin/sh
fl="-std=c89"
for opt; do
  case "$opt" in
    -ansi|-std=c89|-std=iso9899:1990) fl="";;
    -std=*) echo "`basename $0` called with non ANSI/ISO C option $opt" >&2
	    exit 1;;
  esac
done
exec gcc $fl ${1+"$@"}
EOF
  cat > $pkgdir/bin/c99 <<"EOF"
#!/bin/sh
fl="-std=c99"
for opt; do
  case "$opt" in
    -std=c99|-std=iso9899:1999) fl="";;
    -std=*) echo "`basename $0` called with non ISO C99 option $opt" >&2
	    exit 1;;
  esac
done
exec gcc $fl ${1+"$@"}
EOF
  chmod 755 ${pkgdir}/bin/c{8,9}9

  rm -r ${pkgdir}/lib/gcc/$CHOST/${pkgver}/include-fixed
  rm -r ${pkgdir}/lib/gcc/$CHOST/${pkgver}/install-tools
}

package_libgcc() {
  cd ${srcdir}/${pkgbase}-build

  make DESTDIR=${pkgdir} install-target-libgcc
  rm -r ${pkgdir}/lib/gcc/$CHOST/${pkgver}/include
}

package_libstdc++() {
  depends=(libgcc)

  cd ${srcdir}/${pkgbase}-build

  make DESTDIR=${pkgdir} install-target-libstdc++-v3
}
