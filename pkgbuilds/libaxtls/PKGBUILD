pkgname=libaxtls
pkgver=1.4.9
pkgrel=1
pkgdesc="axTLS embedded SSL Library"
arch=('i686' 'x86_64')
url="http://axtls.sf.net/"
license=('BSD')
checkdepends=('perl')
#groups=('base')
source=(http://sourceforge.net/projects/axtls/files/${pkgver}/axTLS-${pkgver}.tar.gz
        config)

prepare() {
  cd "${srcdir}/axTLS"

  sed -i.bak -e "/libs:/s/ \$(TARGET2)//" ssl/Makefile

  cp "${srcdir}/config" "${srcdir}/axTLS/config/.config"
  make oldconfig >/dev/null
}

build() {
  cd "${srcdir}/axTLS"

  make
}

check() {
  cd "${srcdir}/axTLS"

  make test
}

package() {
  cd "${srcdir}/axTLS"

  install -Dm755 _stage/axtlswrap "${pkgdir}/bin/sslwrap"
  install -Dm644 _stage/libaxtls.a "${pkgdir}/lib/libaxtls.a"
  install -dm755 "${pkgdir}/include/axTLS"
  install -m644 ssl/*.h crypto/*.h "${pkgdir}/include/axTLS"
  rm "${pkgdir}/include/axTLS"/{cert,private_key,os_port}.h
  install -Dm644 config/config.h "${pkgdir}/include/axTLS/config.h"
}
