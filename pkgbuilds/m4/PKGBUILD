pkgbase=m4
pkgname=${pkgbase}
pkgrel=1
pkgver=1.4.17
pkgdesc="GNU macro processor"
arch=(i686 x86_64)
license=(GPL3)
url="http://www.gnu.org/software/m4/"
depends=(sh)
source=(http://ftp.gnu.org/gnu/${pkgbase}/${pkgbase}-${pkgver}.tar.xz{,.sig})
sha256sums=('f0543c3beb51fa6b3337d8025331591e0e18d8ec2886ed391f1aade43477d508'
            'SKIP')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  ./configure --build=$CHOST \
    --prefix=
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  # NOTE: 109 and 121 fail because of UTF8-invalid test data
  make check || true
}

package() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make DESTDIR=${pkgdir} install
}
