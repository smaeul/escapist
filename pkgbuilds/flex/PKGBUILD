pkgbase=flex
pkgname=(flex libfl)
pkgrel=1
pkgver=2.5.39
pkgdesc="Lexer generator utility"
arch=(i686 x86_64)
license=(custom)
url="http://flex.sourceforge.net/"
depends=(m4 sh)
makedepends=(yacc)
source=(http://downloads.sourceforge.net/${pkgbase}/${pkgbase}-${pkgver}.tar.xz
        ${pkgbase}-musl.patch)
sha256sums=('c988bb3ab340aaba16df5a54ab98bb4760599975375c8ac9388a078b7f27e9e8'
            '3f50e2e1f5e0bbb4190e5622a02862e30d181a3e138598a4e7ecd6a5673f70ae')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  patch -p1 -i ${srcdir}/${pkgbase}-musl.patch
}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  ./configure --build=$CHOST \
    --prefix= \
    --disable-shared
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make check
}

package_flex() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make DESTDIR=${pkgdir} install
  ln -s flex ${pkgdir}/bin/lex
  rm -r ${pkgdir}/{include,lib}
  install -Dm644 COPYING ${pkgdir}/share/licenses/custom/${pkgname}
}

package_libfl() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  make DESTDIR=${pkgdir} install
  rm -r ${pkgdir}/{bin,share}
  install -Dm644 COPYING ${pkgdir}/share/licenses/custom/${pkgname}
}
