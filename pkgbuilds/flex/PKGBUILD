pkgname=flex
pkgver=2.5.38
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"
arch=('i686' 'x86_64')
url="http://flex.sourceforge.net/"
license=('custom')
depends=('m4' 'sh')
groups=('devel')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        ${pkgname}-musl.patch)

prepare () {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/${pkgname}-musl.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --build=$CHOST --prefix= \
    --disable-shared --disable-nls
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # these tests are incompatible with latest bison
  sed -i.bak -e '/test-bison-yylloc/d' -e '/test-bison-yylval/d' tests/Makefile.in

  make check
}
  
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  ln -s flex "${pkgdir}/bin/lex"
  install -Dm644 COPYING "${pkgdir}/share/licenses/${pkgname}/LICENSE"
}
