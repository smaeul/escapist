pkgbase=mpfr
pkgname=lib${pkgbase}
pkgrel=1
pkgver=3.1.2
_patch=11
pkgdesc="GNU multiprecision floating-point library"
arch=(i686 x86_64)
license=(LGPL)
url="http://www.mpfr.org/"
depends=(libgmp)
source=(http://www.mpfr.org/${pkgbase}-current/${pkgbase}-${pkgver%.${_patch}}.tar.xz{,.asc}
        http://www.mpfr.org/${pkgbase}-current/allpatches
        ${pkgbase}-musl.patch)
sha256sums=('399d0f47ef6608cc01d29ed1b99c7faff36d9994c45f36f41ba250147100453b'
            'SKIP'
            '1b9fdb515efb09a506a01e1eb307b1464455f5ca63d6c193db3a3da371ab3220'
            '3f50e2e1f5e0bbb4190e5622a02862e30d181a3e138598a4e7ecd6a5673f70ae')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver%.${_patch}}

# XXX: Chokes busybox patch
#  patch -p1 -i ${srcdir}/allpatches
  patch -p1 -i ${srcdir}/${pkgbase}-musl.patch
}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver%.${_patch}}

  ./configure --build=$CHOST \
    --prefix= \
    --enable-thread-safe --disable-shared
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver%.${_patch}}

  make check
}

package() {
  cd ${srcdir}/${pkgbase}-${pkgver%.${_patch}}

  make DESTDIR=${pkgdir} install
}
