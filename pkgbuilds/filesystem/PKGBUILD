pkgname=filesystem
pkgrel=1
pkgver=2014.12
pkgdesc="Base filesystem structure"
arch=(any)
groups=(base)
license=(MIT)
url="https://pkg.smaeul.me/"
depends=(iana)
options=(emptydirs)
backup=(etc/crypttab
        etc/fstab
        etc/group
        etc/hostname
        etc/hosts
        etc/issue
        etc/motd
        etc/os-release
        etc/passwd
        etc/profile
        etc/resolv.conf
        etc/shells)
source=(crypttab
        fstab
        group
        hostname
        hosts
        issue
        motd
        os-release
        passwd
        profile
        resolv.conf
        shells)
sha256sums=('f7e36bdb3671ddcd60f2a3bac2e8095fc10a77d2d1405f6431c89c615b53e223'
            '46a6d2e15b0b620cc7b5d7653710bac31139c0e6fc191672fcc23d4c3019d188'
            'b1e8053a486c87e1e039972f8c868e584f3f85663009db9b02fdb45f6ece41bd'
            '004f6baffc3d563e6da6a433ae32c91855e196854b4d557598693f4a9da8e4ab'
            'e9c5670eff924c2e88733aa60f3d49185fe8f32dafc3a14dec64e89a1a44ae9f'
            'b157ebb45f5159c037c2bbac13dc855385ad7ae506c3b8bf8dd96d916dcdc71c'
            'd1b45c0d901b7a0ec1bcab2cbc07994c270b6bb636ee3616cd56268a31ef1c70'
            'fdaecb641f53fd030e593850ce17b9dbe2ae001a855b46b659b0d2102e5cafbb'
            'ef6e2b554e8cf366b12c16c42b8aaeae9dcef8a887e6b2b64201956023f32aba'
            'ba8de1f1987d75309cd6696298eb388e12d629a16cc0bb87032076eb6c3073ba'
            'dac01796d878ba0c8185069c7bd3828fbe2fc91245dc01c021ab2f9f7c67d1b0'
            'c9572f5efeade990942155e935ff8bbc469af71e6b9c53876c028e9d43d29058')

package() {
  install -dm755 ${pkgdir}/{bin,boot,dev,etc/profile.d,home,include,lib,mnt,root,run,srv}
  install -dm755 ${pkgdir}/share/{dict,doc,licenses,locale,man/man{1..8}}
  install -dm755 ${pkgdir}/var/{cache,empty,lib,log,spool/mail}
  install -dm555 ${pkgdir}/{proc,sys}
  install -dm700 ${pkgdir}/root
  install -dm1777 ${pkgdir}/{,var/}tmp
  ln -s / ${pkgdir}/usr
  ln -s /run ${pkgdir}/var/run
  ln -s /run/lock ${pkgdir}/var/lock
  ln -s /var/spool/mail ${pkgdir}/var/mail

  install -m644 ${srcdir}/{fstab,group,hostname,hosts,issue,motd,os-release,passwd,profile,resolv.conf,shells} ${pkgdir}/etc
  install -m600 ${srcdir}/crypttab ${pkgdir}/etc
  ln -s /proc/self/mounts ${pkgdir}/etc/mtab
}
