# Derived from Arch Linux 2014.03.22
# Samuel Holland <samuel@sholland.net>

pkgname=filesystem
pkgver=2014.03
pkgrel=1
pkgdesc='Base filesystem'
arch=('i686' 'x86_64')
license=('GPL') # Arch had this license; must start from scratch or keep GPL
url='http://sholland.net'
groups=('base')
#install='filesystem.install'
depends=('iana-etc')
options=('emptydirs')
backup=('etc/fstab' 'etc/crypttab' 'etc/group' 'etc/hosts' 'etc/passwd'
        'etc/shadow' 'etc/gshadow' 'etc/resolv.conf' 'etc/motd' 'etc/nsswitch.conf'
        'etc/shells' 'etc/host.conf' 'etc/securetty' 'etc/profile' 'etc/issue')
source=('group' 'issue' 'nsswitch.conf' 'securetty' 'host.conf' 'passwd' 'shadow'
        'fstab' 'crypttab' 'hosts' 'motd' 'os-release' 'resolv.conf' 'shells'
        'gshadow' 'profile' 'modprobe.d.usb-load-ehci-first' 'locale.sh')

package() {
	cd "${pkgdir}"

	#
	# setup root filesystem
	#
	for d in bin boot dev etc home include lib mnt share/{doc,man,misc} var opt src srv run; do
		install -d -m755 ${d}
	done
	for d in proc sys; do
		install -d -m555 ${d}
	done
	install -d -m0750 root
	install -d -m1777 tmp
	#ln -s lib ${pkgdir}/lib64
	#ln -s bin ${pkgdir}/sbin

	# setup /etc
	install -d -m755 etc/{skel,profile.d}
	for f in fstab group host.conf hosts issue motd nsswitch.conf os-release passwd resolv.conf securetty shells profile; do
		install -m644 ${srcdir}/${f} etc/
	done
	ln -s /proc/self/mounts etc/mtab
	for f in gshadow shadow crypttab; do
		install -m600 ${srcdir}/${f} etc/
	done
	touch etc/static-release
	install -D -m644 ${srcdir}/modprobe.d.usb-load-ehci-first lib/modprobe.d/usb-load-ehci-first.conf
	install -m755 ${srcdir}/locale.sh etc/profile.d/locale.sh

	# setup /share
	for d in $(seq 8); do
		install -d -m755 share/man/man${d}
	done

	# setup /srv
	# vsftpd won't run with write perms on /srv/ftp
	install -d -m555 -g ftp srv/ftp
	install -d -m755 -g http srv/http

	# setup /var
	for d in cache/man local opt log/old lib/misc empty; do
		install -d -m755 var/${d}
	done
	install -d -m1777 var/{tmp,spool/mail}
	# allow setgid games to write scores
	install -d -m775 -g games var/games
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	# setup /local
	for d in bin etc games include lib man sbin share src; do
		install -d -m755 local/${d}
	done
	ln -s ../man local/share/man
}
