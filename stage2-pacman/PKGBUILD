# Maintainer: Samuel Holland <samuel@sholland.net>

pkgname=stage2-pacman
pkgver=4.1.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('stage2-sh' 'stage2-curl')
optdepends=('stage2-fakeroot: for makepkg usage as normal user')
makedepends=('stage1-toolchain' 'stage2-libarchive')
backup=(stage1/etc/pacman.conf stage1/etc/makepkg.conf)
source=(ftp://ftp.archlinux.org/other/pacman/pacman-$pkgver.tar.gz{,.sig}
        pacman.conf.i686
        pacman.conf.x86_64
        makepkg.conf)
md5sums=('063c8b0ff6bdf903dc235445525627cd'
         'SKIP'
         '2db6c94709bb30cc614a176ecf8badb1'
         'de74a13618347f08ae4a9637f74471c4'
         'f4940a8003ebd11e514827a790ef4e7f')

build() {
  cd $srcdir/pacman-$pkgver

  ./configure --prefix=/stage1 --sysconfdir=/stage1/etc \
    --localstatedir=/stage1/var --disable-doc \
    --with-scriptlet-shell=/stage1/bin/mksh \
    --without-openssl

  make
  make -C contrib
}

package() {
  cd $srcdir/pacman-$pkgver
  make DESTDIR=$pkgdir install
  make DESTDIR=$pkgdir -C contrib install

  # install Arch specific stuff
  install -dm755 $pkgdir/stage1/etc
  install -m644 $srcdir/pacman.conf.$CARCH $pkgdir/stage1/etc/pacman.conf

  case "$CARCH" in
    i686)
      mycarch="i686"
      mychost="i686-pc-linux-gnu"
      myflags="-march=i686"
      ;;
    x86_64)
      mycarch="x86_64"
      mychost="x86_64-unknown-linux-gnu"
      myflags="-march=x86-64"
      ;;
  esac
  install -m644 $srcdir/makepkg.conf $pkgdir/stage1/etc/
  # set things correctly in the default conf file
  sed -i $pkgdir/stage1/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"

  # put bash_completion in the right location
  install -dm755 ${pkgdir}/stage1/share/bash-completion/completions
  mv ${pkgdir}/stage1/etc/bash_completion.d/pacman \
    ${pkgdir}/stage1/share/bash-completion/completions
  rmdir ${pkgdir}/stage1/bash_completion.d

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/stage1/share/bash-completion/completions/$f"
  done
}
