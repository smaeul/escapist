# Maintainer: Samuel Holland <samuel@sholland.net>

pkgname=stage0-binutils
pkgver=2.23.2
pkgrel=1
pkgdesc="Temporary static assembler and friends - Starch stage 0"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
source=(http://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        binutils-2.23.2-texinfo-5.0.patch)
md5sums=('4f8fa651e35ef262edc01d60fb45702e'
         'dfde4428f08d91f309cdcfe92bf28d08'
         '34e439ce23213a91e2af872dfbb5094c')

prepare() {
  cd ${srcdir}/binutils-${pkgver}

  # Patch to recognize musl as a valid libc
  sed -i "s#\(-\?lin\)\(ux-gnu\* |\)\( -\?linux-android\*\)#\1\2 \1ux-musl* |\3#g" config.sub

  # http://sourceware.org/git/?p=binutils.git;a=patch;h=e02bf935
  # http://sourceware.org/git/?p=binutils.git;a=patch;h=935f8542
  patch -p1 -i ${srcdir}/binutils-2.23.2-texinfo-5.0.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  rm -rf ${srcdir}/binutils-build
  mkdir ${srcdir}/binutils-build
}

build() {
  cd ${srcdir}/binutils-build

  ${srcdir}/binutils-${pkgver}/configure \
    --target=$CHOST --prefix=/stage0 \
    --with-sysroot=/stage1 --with-lib-path=/stage1/lib \
    --disable-shared --disable-werror --disable-multilib --disable-nls

  make tooldir=/stage0
}

package() {
  cd ${srcdir}/binutils-build

  make prefix=${pkgdir}/stage0 tooldir=${pkgdir}/stage0 install
}
