# Maintainer: Samuel Holland <samuel@sholland.net>

pkgname=stage2-readline
_basever=6.2
_patchlevel=004 #prepare for some patches
pkgver=$_basever.$_patchlevel
pkgrel=2
pkgdesc="GNU readline library"
arch=('i686' 'x86_64')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
makedepends=('stage2-ncurses' 'stage1-toolchain')
backup=('stage1/etc/inputrc')
options=('!emptydirs')
source=(http://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz{,.sig}
        inputrc)
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi
md5sums=('67948acb2ca081f23359d0256e9a271c'
         '928f7d248320a65e43c2dc427e99582b'
         'SKIP'
         '83287d52a482f790dfb30ec0a8746669'
         '8e6a51e2e0e6e45a82752e3692c111ac'
         '0665020ea118e8434bd145fb71f452cc'
         '285361ca6d48c51ae2428157e174e812'
         'c9d5d79718856e711667dede87cb7622'
         '4437205bb1462f5f32e4812b8292c675'
         'c08e787f50579ce301075c523fa660a4'
         '7e39cad1d349b8ae789e4fc33dbb235f')

prepare() {
  cd ${srcdir}/readline-$_basever

  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" $p)"
    patch -p0 -i $srcdir/readline${_basever//./}-$(printf "%03d" $p)
  done

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

  sed -i "s#\(-\?lin\)\(ux-newlib\* |\)\( -\?linux-uclibc\*\)#\1\2 \1ux-musl* |\3#g" \
    support/config.sub
}

build() {
  cd ${srcdir}/readline-$_basever

  # build with -fPIC for x86_64 (FS#15634)
  [[ $CARCH == "x86_64" ]] && CFLAGS="$CFLAGS -fPIC"

  ./configure \
    --build=$CHOST --prefix=/stage1 \
    --disable-shared

  make
}

package() {
  cd ${srcdir}/readline-$_basever
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/inputrc ${pkgdir}/stage1/etc/inputrc
}
