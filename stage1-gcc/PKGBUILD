# Maintainer: Samuel Holland <samuel@sholland.net>

pkgname=('stage1-gcc')
pkgver=4.8.1
pkgrel=1
pkgdesc="Temporary static GCC and libs - Starch stage 1"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
url="http://gcc.gnu.org"
depends=('stage1-binutils' 'stage1-linux-headers' 'stage1-musl')
makedepends=('stage1-gmp' 'stage1-mpc' 'stage1-mpfr')
conflicts=('stage0-libstdcpp')
options=('staticlibs' '!emptydirs')
source=(http://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
        gcc-4.8.1-musl.patch)
md5sums=(SKIP
         SKIP)

prepare() {
  cd ${srcdir}/gcc-${pkgver}

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # No need for lib64 folder; no multilib in stage0
  [[ $CARCH == "x86_64" ]] && sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

  # https://github.com/GregorR/musl-cross
  patch -p1 -i ${srcdir}/gcc-4.8.1-musl.patch

  for file in \
    $(find gcc/config -name linux64.h -o -name linux.h -o -name sysv4.h)
  do
    sed -e 's@/lib\(64\)\?\(32\)\?/ld@/stage1&@g' \
        -e 's@/usr@/stage1@g' -i $file
    echo '
#undef STANDARD_STARTFILE_PREFIX_1
#undef STANDARD_STARTFILE_PREFIX_2
#define STANDARD_STARTFILE_PREFIX_1 "/stage1/lib/"
#define STANDARD_STARTFILE_PREFIX_2 ""' >> $file
  done

  rm -rf ${srcdir}/gcc-build
  mkdir ${srcdir}/gcc-build
}

build() {
  cd ${srcdir}/gcc-build

  # using -pipe causes spurious test-suite failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=48565
  CFLAGS=${CFLAGS/-pipe/}
  CXXFLAGS=${CXXFLAGS/-pipe/}

  ${srcdir}/gcc-${pkgver}/configure \
    --host=$CHOST --target=$CHOST --prefix=/stage1 --libexecdir=/stage1/lib \
    --disable-shared --disable-multilib --enable-threads=posix --disable-werror \
    --disable-nls --disable-libgomp --disable-libmudflap --disable-libsanitizer \
    --enable-clocale=generic --enable-languages=c,c++ --disable-libstdcxx-pch \
    --with-local-prefix=/stage1 --with-native-system-header-dir=/stage1/include \
    --disable-libquadmath --disable-libitm

#    --with-gmp=/stage0 --with-mpfr=/stage0 --with-mpc=/stage0 \
#    --with-newlib --without-headers
#    --with-sysroot=/stage1 \
#    --disable-libssp --disable-libitm --disable-libatomic --disable-libquadmath \
#    --disable-decimal-float \


  make
}

package() {
  cd ${srcdir}/gcc-build

  make DESTDIR=${pkgdir} install

  ln -s gcc ${pkgdir}/stage1/bin/cc
  ln -s libgcc.a ${pkgdir}/stage1/lib/gcc/$CHOST/${pkgver}/libgcc_eh.a

  rm ${pkgdir}/stage1/lib/libiberty.a
}
