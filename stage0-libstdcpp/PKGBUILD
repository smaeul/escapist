# Maintainer: Samuel Holland <samuel@sholland.net>

pkgname=('stage0-libstdcpp')
pkgver=4.8.1
pkgrel=1
pkgdesc="Temporary static libstdc++ - Starch stage 0"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gcc.gnu.org"
makedepends=('stage0-binutils' 'stage0-gcc')
options=('!emptydirs')
source=(http://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
        gcc-4.8.1-musl.patch)
md5sums=(SKIP
         SKIP)

prepare() {
  cd ${srcdir}/gcc-${pkgver}

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # No need for lib64 folder; no multilib in stage0
  [[ $CARCH == "x86_64" ]] && sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

  # https://github.com/GregorR/musl-cross
  patch -p1 -i ${srcdir}/gcc-4.8.1-musl.patch

  rm -rf ${srcdir}/gcc-build
  mkdir ${srcdir}/gcc-build
}

build() {
  cd ${srcdir}/gcc-build

  # using -pipe causes spurious test-suite failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=48565
  CFLAGS=${CFLAGS/-pipe/}
  CXXFLAGS=${CXXFLAGS/-pipe/}

  ${srcdir}/gcc-${pkgver}/libstdc++-v3/configure \
    --host=${CHOST} \
    --prefix=/stage1 --libexecdir=/stage1/lib \
    --with-gxx-include-dir=/stage0/$CHOST/include/c++/${pkgver} \
    --disable-shared --disable-multilib --disable-nls \
    --disable-libstdcxx-pch --disable-libstdcxx-threads

  make
}

package() {
  cd ${srcdir}/gcc-build

  make -j1 DESTDIR=${pkgdir} install
}
